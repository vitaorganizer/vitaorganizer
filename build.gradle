group 'com.soywiz'
version new File('resources/currentVersion.txt').text

buildscript {
    ext.version = new File('resources/currentVersion.txt').text
    ext.kotlin_version = '1.0.3'
    ext.proguard_version = '5.2.1'
    ext.launch4j_version = '1.6.2'

    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.sf.proguard:proguard-gradle:$proguard_version"
        classpath "gradle.plugin.edu.sc.seis.gradle:launch4j:$launch4j_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'edu.sc.seis.launch4j'

mainClassName = 'com.soywiz.vitaorganizer.VitaOrganizer'
compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.5

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src', 'ftp4j/src']
    main.resources.srcDirs = ['resources', 'assets']
    test.java.srcDirs = ['test']
    test.kotlin.srcDirs = ['test']
    test.resources.srcDirs = ['testresources', 'testassets']
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //compile "commons-net:commons-net:3.5"
    testCompile "junit:junit:4.11"
}

jar {
    manifest {
        attributes("Main-Class": "com.soywiz.vitaorganizer.VitaOrganizer")
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task minimizedJar(type: proguard.gradle.ProGuardTask) {
    print "build/libs/vitaorganizer-${version}.jar"
    injars "build/libs/vitaorganizer-${version}.jar"
    outjars "build/libs/vitaorganizer-${version}.min.jar"
    libraryjars System.properties.'java.home' + "/lib/rt.jar"
    printmapping "build/libs/vitaorganizer-${version}.map"
    ignorewarnings
    //dontobfuscate
    keep access: 'public',
            name: 'com.soywiz.vitaorganizer.VitaOrganizer', {
        method access: 'public static',
                type: 'void',
                name: 'main',
                parameters: 'java.lang.String[]'
    }
}

minimizedJar.dependsOn jar

copyL4jLib.dependsOn minimizedJar

launch4j {
    print "version: $version"
    outfile = "../../build/libs/vitaorganizer-${ext.version}.exe"
    icon = "../../extra/icon.ico"
    mainClassName = project.mainClassName
    //copyConfigurable = project.tasks.fatJar.outputs.files
    //jar = "lib/${project.tasks.fatJar.archiveName}"
    jar = "../../build/libs/vitaorganizer-${ext.version}.min.jar"
}
